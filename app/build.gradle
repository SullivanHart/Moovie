plugins {
    id 'com.android.application'
    id 'com.google.gms.google-services'
}

android {
    namespace 'com.moovie'

    compileSdkVersion 34
    defaultConfig {
        applicationId 'com.moovie'
        minSdkVersion 24
        targetSdkVersion 33
        versionCode 1
        versionName '1.0'

        multiDexEnabled true
        vectorDrawables.useSupportLibrary true
    }

    buildFeatures {
        buildConfig = true
    }

    buildTypes {
        debug {
            buildConfigField "String", "TMDB_API_KEY_RA", "\"${getLocalProperty('TMDB_API_KEY_RA')}\""
            buildConfigField "String", "TMDB_API_KEY", "\"${getLocalProperty('TMDB_API_KEY')}\""
            buildConfigField "String", "WM_API_KEY", "\"${getLocalProperty('WM_API_KEY')}\""
        }
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            buildConfigField "String", "TMDB_API_KEY_RA", "\"${getLocalProperty('TMDB_API_KEY_RA')}\""
            buildConfigField "String", "TMDB_API_KEY", "\"${getLocalProperty('TMDB_API_KEY')}\""
            buildConfigField "String", "WM_API_KEY", "\"${getLocalProperty('WM_API_KEY')}\""
        }
    }
}

afterEvaluate {
    tasks.register('androidJavadocs', Javadoc) {

        // Depend on tasks that generate BuildConfig
        dependsOn 'assembleRelease'

        // Collect all Java source files
        def sourceDirs = android.sourceSets.main.java.srcDirs
        def buildConfigDir = file("$buildDir/generated/source/buildConfig/release")

        // Add all source directories
        source = sourceDirs
        if (buildConfigDir.exists()) {
            source += buildConfigDir
        }

        // Android runtime (android.jar)
        classpath += files(android.getBootClasspath())

        // Add dependencies from the release variant
        def releaseVariant = android.applicationVariants.find { it.name == "release" }
        if (releaseVariant != null) {
            classpath += files(releaseVariant.getCompileClasspath())
        }

        // Output - use project-relative path
        destinationDir = file("${projectDir}/docs")

        // Avoid hard fail on warnings
        failOnError false

        // Javadoc options
        options {
            encoding = 'UTF-8'
            charSet = 'UTF-8'
            author = true
            version = true
            links("https://docs.oracle.com/javase/8/docs/api/")
            // Suppress all warnings and linting
            addStringOption('Xdoclint:none', '-quiet')
            // Show private and package-private members too
            memberLevel = JavadocMemberLevel.PRIVATE
        }
    }
}


// Helper function to read local.properties
def getLocalProperty(String key) {
    Properties properties = new Properties()
    File localPropertiesFile = rootProject.file('local.properties')
    if (localPropertiesFile.exists()) {
        properties.load(localPropertiesFile.newInputStream())
        return properties.getProperty(key, "")
    }
    return ""
}

dependencies {
    // Firestore
    implementation 'com.google.firebase:firebase-firestore:23.0.3'

    // Firebase
    implementation 'com.google.firebase:firebase-auth:21.0.1'
    implementation 'com.google.android.gms:play-services-auth:19.2.0'

    // FirebaseUI
    implementation 'com.firebaseui:firebase-ui-auth:8.0.0'

    // Support Libs
    implementation 'androidx.appcompat:appcompat:1.3.1'
    implementation 'androidx.vectordrawable:vectordrawable-animated:1.1.0'
    implementation 'androidx.cardview:cardview:1.0.0'
    implementation 'androidx.browser:browser:1.0.0'
    implementation 'com.google.android.material:material:1.4.0'
    implementation 'androidx.multidex:multidex:2.0.1'
    implementation 'androidx.recyclerview:recyclerview:1.2.1'

    // Architecture components
    implementation 'androidx.lifecycle:lifecycle-runtime:2.3.1'
    implementation 'androidx.lifecycle:lifecycle-extensions:2.2.0'
    implementation 'androidx.navigation:navigation-fragment:2.9.6'
    implementation 'androidx.navigation:navigation-ui:2.9.6'
    implementation 'androidx.navigation:navigation-runtime-android:2.9.6'
    annotationProcessor 'androidx.lifecycle:lifecycle-compiler:2.3.1'

    // Third-party
    implementation 'me.zhanghai.android.materialratingbar:library:1.4.0'
    implementation 'com.github.bumptech.glide:glide:4.12.0'

    // Retrofit
    implementation 'com.squareup.retrofit2:retrofit:2.9.0'
    implementation 'com.squareup.retrofit2:converter-gson:2.9.0'

    // Navigation
    implementation 'androidx.navigation:navigation-fragment:2.5.3'
    implementation 'androidx.navigation:navigation-ui:2.5.3'
}